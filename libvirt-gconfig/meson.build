libvirt_gconfig_public_headers = [
  'libvirt-gconfig.h',
  'libvirt-gconfig-main.h',
  'libvirt-gconfig-object.h',
  'libvirt-gconfig-capabilities.h',
  'libvirt-gconfig-capabilities-host.h',
  'libvirt-gconfig-capabilities-cpu.h',
  'libvirt-gconfig-capabilities-cpu-feature.h',
  'libvirt-gconfig-capabilities-cpu-model.h',
  'libvirt-gconfig-capabilities-cpu-topology.h',
  'libvirt-gconfig-capabilities-guest.h',
  'libvirt-gconfig-capabilities-guest-arch.h',
  'libvirt-gconfig-capabilities-guest-domain.h',
  'libvirt-gconfig-capabilities-guest-feature.h',
  'libvirt-gconfig-capabilities-host-secmodel.h',
  'libvirt-gconfig-domain.h',
  'libvirt-gconfig-domain-address.h',
  'libvirt-gconfig-domain-address-pci.h',
  'libvirt-gconfig-domain-address-usb.h',
  'libvirt-gconfig-domain-capabilities.h',
  'libvirt-gconfig-domain-capabilities-os.h',
  'libvirt-gconfig-domain-channel.h',
  'libvirt-gconfig-domain-chardev.h',
  'libvirt-gconfig-domain-chardev-source.h',
  'libvirt-gconfig-domain-chardev-source-pty.h',
  'libvirt-gconfig-domain-chardev-source-spiceport.h',
  'libvirt-gconfig-domain-chardev-source-spicevmc.h',
  'libvirt-gconfig-domain-chardev-source-unix.h',
  'libvirt-gconfig-domain-clock.h',
  'libvirt-gconfig-domain-console.h',
  'libvirt-gconfig-domain-controller.h',
  'libvirt-gconfig-domain-controller-usb.h',
  'libvirt-gconfig-domain-cpu.h',
  'libvirt-gconfig-domain-cpu-feature.h',
  'libvirt-gconfig-domain-cpu-model.h',
  'libvirt-gconfig-domain-device.h',
  'libvirt-gconfig-domain-disk.h',
  'libvirt-gconfig-domain-disk-driver.h',
  'libvirt-gconfig-domain-filesys.h',
  'libvirt-gconfig-domain-graphics.h',
  'libvirt-gconfig-domain-graphics-desktop.h',
  'libvirt-gconfig-domain-graphics-rdp.h',
  'libvirt-gconfig-domain-graphics-sdl.h',
  'libvirt-gconfig-domain-graphics-spice.h',
  'libvirt-gconfig-domain-graphics-vnc.h',
  'libvirt-gconfig-domain-hostdev.h',
  'libvirt-gconfig-domain-hostdev-pci.h',
  'libvirt-gconfig-domain-input.h',
  'libvirt-gconfig-domain-interface.h',
  'libvirt-gconfig-domain-interface-bridge.h',
  'libvirt-gconfig-domain-interface-filterref.h',
  'libvirt-gconfig-domain-interface-filterref-parameter.h',
  'libvirt-gconfig-domain-interface-network.h',
  'libvirt-gconfig-domain-interface-user.h',
  'libvirt-gconfig-domain-memballoon.h',
  'libvirt-gconfig-domain-os.h',
  'libvirt-gconfig-domain-parallel.h',
  'libvirt-gconfig-domain-power-management.h',
  'libvirt-gconfig-domain-redirdev.h',
  'libvirt-gconfig-domain-seclabel.h',
  'libvirt-gconfig-domain-serial.h',
  'libvirt-gconfig-domain-smartcard.h',
  'libvirt-gconfig-domain-smartcard-host.h',
  'libvirt-gconfig-domain-smartcard-host-certificates.h',
  'libvirt-gconfig-domain-smartcard-passthrough.h',
  'libvirt-gconfig-domain-snapshot.h',
  'libvirt-gconfig-domain-snapshot-disk.h',
  'libvirt-gconfig-domain-sound.h',
  'libvirt-gconfig-domain-timer.h',
  'libvirt-gconfig-domain-timer-hpet.h',
  'libvirt-gconfig-domain-timer-pit.h',
  'libvirt-gconfig-domain-timer-rtc.h',
  'libvirt-gconfig-domain-video.h',
  'libvirt-gconfig-helpers.h',
  'libvirt-gconfig-interface.h',
  'libvirt-gconfig-network.h',
  'libvirt-gconfig-network-filter.h',
  'libvirt-gconfig-node-device.h',
  'libvirt-gconfig-secret.h',
  'libvirt-gconfig-storage-permissions.h',
  'libvirt-gconfig-storage-pool.h',
  'libvirt-gconfig-storage-pool-source.h',
  'libvirt-gconfig-storage-pool-target.h',
  'libvirt-gconfig-storage-vol.h',
  'libvirt-gconfig-storage-vol-backing-store.h',
  'libvirt-gconfig-storage-vol-target.h',
]

libvirt_gconfig_sources = [
  'libvirt-gconfig-object.c',
  'libvirt-gconfig-main.c',
  'libvirt-gconfig-capabilities.c',
  'libvirt-gconfig-capabilities-host.c',
  'libvirt-gconfig-capabilities-cpu.c',
  'libvirt-gconfig-capabilities-cpu-feature.c',
  'libvirt-gconfig-capabilities-cpu-model.c',
  'libvirt-gconfig-capabilities-cpu-topology.c',
  'libvirt-gconfig-capabilities-guest.c',
  'libvirt-gconfig-capabilities-guest-arch.c',
  'libvirt-gconfig-capabilities-guest-domain.c',
  'libvirt-gconfig-capabilities-guest-feature.c',
  'libvirt-gconfig-capabilities-host-secmodel.c',
  'libvirt-gconfig-domain.c',
  'libvirt-gconfig-domain-address.c',
  'libvirt-gconfig-domain-address-pci.c',
  'libvirt-gconfig-domain-address-usb.c',
  'libvirt-gconfig-domain-capabilities.c',
  'libvirt-gconfig-domain-capabilities-os.c',
  'libvirt-gconfig-domain-channel.c',
  'libvirt-gconfig-domain-chardev.c',
  'libvirt-gconfig-domain-chardev-source.c',
  'libvirt-gconfig-domain-chardev-source-pty.c',
  'libvirt-gconfig-domain-chardev-source-spiceport.c',
  'libvirt-gconfig-domain-chardev-source-spicevmc.c',
  'libvirt-gconfig-domain-chardev-source-unix.c',
  'libvirt-gconfig-domain-clock.c',
  'libvirt-gconfig-domain-console.c',
  'libvirt-gconfig-domain-controller.c',
  'libvirt-gconfig-domain-controller-usb.c',
  'libvirt-gconfig-domain-cpu.c',
  'libvirt-gconfig-domain-cpu-feature.c',
  'libvirt-gconfig-domain-cpu-model.c',
  'libvirt-gconfig-domain-device.c',
  'libvirt-gconfig-domain-disk.c',
  'libvirt-gconfig-domain-disk-driver.c',
  'libvirt-gconfig-domain-filesys.c',
  'libvirt-gconfig-domain-graphics.c',
  'libvirt-gconfig-domain-graphics-desktop.c',
  'libvirt-gconfig-domain-graphics-rdp.c',
  'libvirt-gconfig-domain-graphics-sdl.c',
  'libvirt-gconfig-domain-graphics-spice.c',
  'libvirt-gconfig-domain-graphics-vnc.c',
  'libvirt-gconfig-domain-hostdev.c',
  'libvirt-gconfig-domain-hostdev-pci.c',
  'libvirt-gconfig-domain-input.c',
  'libvirt-gconfig-domain-interface.c',
  'libvirt-gconfig-domain-interface-bridge.c',
  'libvirt-gconfig-domain-interface-filterref.c',
  'libvirt-gconfig-domain-interface-filterref-parameter.c',
  'libvirt-gconfig-domain-interface-network.c',
  'libvirt-gconfig-domain-interface-user.c',
  'libvirt-gconfig-domain-memballoon.c',
  'libvirt-gconfig-domain-os.c',
  'libvirt-gconfig-domain-parallel.c',
  'libvirt-gconfig-domain-power-management.c',
  'libvirt-gconfig-domain-redirdev.c',
  'libvirt-gconfig-domain-seclabel.c',
  'libvirt-gconfig-domain-serial.c',
  'libvirt-gconfig-domain-smartcard.c',
  'libvirt-gconfig-domain-smartcard-host.c',
  'libvirt-gconfig-domain-smartcard-host-certificates.c',
  'libvirt-gconfig-domain-smartcard-passthrough.c',
  'libvirt-gconfig-domain-snapshot.c',
  'libvirt-gconfig-domain-snapshot-disk.c',
  'libvirt-gconfig-domain-sound.c',
  'libvirt-gconfig-domain-timer.c',
  'libvirt-gconfig-domain-timer-hpet.c',
  'libvirt-gconfig-domain-timer-pit.c',
  'libvirt-gconfig-domain-timer-rtc.c',
  'libvirt-gconfig-domain-video.c',
  'libvirt-gconfig-helpers.c',
  'libvirt-gconfig-interface.c',
  'libvirt-gconfig-network.c',
  'libvirt-gconfig-network-filter.c',
  'libvirt-gconfig-node-device.c',
  'libvirt-gconfig-secret.c',
  'libvirt-gconfig-storage-permissions.c',
  'libvirt-gconfig-storage-pool.c',
  'libvirt-gconfig-storage-pool-source.c',
  'libvirt-gconfig-storage-pool-target.c',
  'libvirt-gconfig-storage-vol.c',
  'libvirt-gconfig-storage-vol-backing-store.c',
  'libvirt-gconfig-storage-vol-target.c',
  'libvirt-gconfig-xml-doc.c',
]

libvirt_gconfig_includedir = join_paths(includedir, 'libvirt-gconfig-1.0', 'libvirt-gconfig')

libvirt_gconfig_enum_headers = gnome.mkenums(
  'libvirt-gconfig-enum-types.h',
  sources: libvirt_gconfig_public_headers,
  h_template: 'libvirt-gconfig-enum-types.h.template',
  symbol_prefix: 'gvir_config',
  identifier_prefix: 'GVirConfig',
  install_header: true,
  install_dir: libvirt_gconfig_includedir,
)

libvirt_gconfig_enum_sources = gnome.mkenums(
  'libvirt-gconfig-enum-types.c',
  sources: libvirt_gconfig_public_headers,
  c_template: 'libvirt-gconfig-enum-types.c.template',
  symbol_prefix: 'gvir_config',
  identifier_prefix: 'GVirConfig',
)

libvirt_gconfig_sources += [libvirt_gconfig_enum_sources, libvirt_gconfig_enum_headers]

libvirt_gconfig_deps = [
  libvirt_dep, gobject_dep, libxml_dep,
]

libvirt_gconfig_link_args = common_link_args
if host_machine.system() == 'linux'
  libvirt_gconfig_link_args += [
    '-Wl,--version-script,@0@/@1@'.format(
      meson.current_source_dir(),
      'libvirt-gconfig.sym'
    )
  ]
endif

libvirt_gconfig = shared_library(
  'virt-gconfig-1.0',
  sources: libvirt_gconfig_sources,
  c_args: common_cflags,
  dependencies: libvirt_gconfig_deps,
  include_directories: top_include_dir,
  link_with: [libvirt_glib],
  link_args: libvirt_gconfig_link_args,
  version: libvirt_glib_version,
  darwin_versions: libvirt_glib_darwin_versions,
  soversion: libvirt_glib_soversion,
  install: true,
  install_rpath: rpathdir,
)

libvirt_gconfig_dep = declare_dependency(
  sources: [
    libvirt_gconfig_enum_headers,
  ],
  link_with: libvirt_gconfig,
  dependencies: libvirt_gconfig_deps,
)

install_data(
  libvirt_gconfig_public_headers,
  install_dir: libvirt_gconfig_includedir,
)

if enable_introspection
  libvirt_gconfig_gir_sources = [
    libvirt_gconfig_public_headers,
    libvirt_gconfig_sources,
  ]

  libvirt_gconfig_gir = gnome.generate_gir(
    libvirt_gconfig,
    sources: libvirt_gconfig_gir_sources,
    nsversion: '1.0',
    namespace: 'LibvirtGConfig',
    export_packages: 'libvirt-gconfig-1.0',
    includes: [
      'GObject-2.0',
      'libxml2-2.0',
    ],
    dependencies: libvirt_gconfig_deps,
    header: 'libvirt-gconfig/libvirt-gconfig.h',
    identifier_prefix: 'GVirConfig',
    symbol_prefix: 'gvir_config',
    install: true,
    install_dir_gir: girdir,
    install_dir_typelib: typelibdir,
  )

  libvirt_gconfig_gir_dep = declare_dependency(
    sources: libvirt_gconfig_gir,
  )

  if enable_vapi
    libvirt_gconfig_vapi = gnome.generate_vapi(
      'libvirt-gconfig-1.0',
      install: true,
      packages: [
        'gobject-2.0',
        'libxml-2.0',
      ],
      sources: libvirt_gconfig_gir[0],
    )
  endif
endif

if build_tests
  subdir('tests')
endif

test('check-gconfig-symfile',
     python3,
     args: [check_symfile,
            join_paths(meson.current_source_dir(), 'libvirt-gconfig.sym'),
            join_paths(meson.current_build_dir(), 'libvirt-gconfig-1.0.so')])

test('check-gconfig-symsorting',
     python3,
     args: [check_symsorting,
            join_paths(meson.current_source_dir(), 'libvirt-gconfig.sym')])
